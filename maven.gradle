apply plugin: 'maven'

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

uploadArchives() {
    repositories {
        mavenDeployer {
            repository(url: MAVEN_REPO_URL) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            snapshotRepository(url: MAVEN_SNAPSHOT_REPO_URL) {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }

            pom.groupId = POM_GROUP_ID
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = POM_VERSION

            pom.project {
                name POM_NAME
                packaging POM_PACKAGING
                description POM_DESCRIPTION
                url POM_SITE_URL

                scm {
                    url POM_SCM_URL
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_DEV_CONNECTION
                }

                licenses {
                    license {
                        name POM_LICENSE_NAME
                        url POM_LICENSE_URL
                        distribution POM_LICENSE_DISTRIBUTION
                    }
                }

                developers {
                    developer {
                        id POM_DEVELOPER_ID
                        name POM_DEVELOPER_NAME
                    }
                }
            }
        }
    }
}

task debugPomInfo() {
    doLast {
        println "Maven Parameter(uploadArchives) is:"
        println String.format('\t\t%1$s: %2$s', "MAVEN_REPO_URL", MAVEN_REPO_URL)
        println String.format('\t\t%1$s: %2$s', "MAVEN_SNAPSHOT_REPO_URL", MAVEN_SNAPSHOT_REPO_URL)
        println String.format('\t\t%1$s: %2$s', "POM_GROUP_ID", POM_GROUP_ID)
        println String.format('\t\t%1$s: %2$s', "POM_ARTIFACT_ID", POM_ARTIFACT_ID)
        println String.format('\t\t%1$s: %2$s', "POM_VERSION", POM_VERSION)
        println String.format('\t\tcompile \'%1$s:%2$s:%3$s\'', POM_GROUP_ID, POM_ARTIFACT_ID, POM_VERSION)
    }
}

