apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.android.library'

def siteUrl = 'https://github.com/DovSnier/toolchainkit.git'
def gitUrl = 'git@github.com:DovSnier/toolchainkit.git'

android {
    compileSdkVersion rootProject.ext.compile_sdk_version

    defaultConfig {
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode rootProject.ext.common_version_code
        versionName rootProject.ext.common_version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(path: ':library:base')
    //noinspection GradleDependency
    implementation "com.android.support:appcompat-v7:${rootProject.ext.support_appcompat_version}"
    //noinspection GradleDependency
    implementation "com.android.support:support-annotations:${rootProject.ext.support_annotations_version}"
    testImplementation "junit:junit:${rootProject.ext.junit_version}"
    androidTestImplementation("com.android.support.test:runner:${rootProject.ext.runner_version}") {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support.test', module: 'monitor'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.ext.espresso_core_version}"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def user = properties.getProperty("bintray.user")
def key = properties.getProperty("bintray.apikey")
publish {
    repoName = 'AndroidComponent'
    userOrg = 'dovsnier'
    groupId = 'com.dvsnier'
    artifactId = 'common'
    publishVersion = rootProject.ext.common_version_name
    desc = "the android lib that version is ${rootProject.ext.common_version_name}"
    website = siteUrl
    repository = gitUrl
    autoPublish = true
    bintrayUser = user
    bintrayKey = key
}
