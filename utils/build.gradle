apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'

def siteUrl = 'https://github.com/DovSnier/toolchainkit.git'
def gitUrl = 'git@github.com:DovSnier/toolchainkit.git'

android {
    compileSdkVersion rootProject.ext.compile_sdk_version

    defaultConfig {
        minSdkVersion rootProject.ext.min_sdk_version
        targetSdkVersion rootProject.ext.target_sdk_version
        versionCode rootProject.ext.utils_version_code
        versionName rootProject.ext.utils_version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //noinspection GradleDependency
    implementation "com.android.support:appcompat-v7:${rootProject.ext.support_appcompat_version}"
    //noinspection GradleDependency
    implementation "com.android.support:support-annotations:${rootProject.ext.support_annotations_version}"
    implementation "com.google.code.gson:gson:${rootProject.ext.gson_version}"
    testImplementation "junit:junit:${rootProject.ext.junit_version}"
    androidTestImplementation "com.android.support.test:runner:${rootProject.ext.runner_version}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${rootProject.ext.espresso_core_version}"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    //noinspection GroovyAccessibility
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def user = properties.getProperty("bintray.user")
def key = properties.getProperty("bintray.apikey")
publish {
    repoName = 'AndroidComponent'
    userOrg = 'dovsnier'
    groupId = 'com.dvsnier'
    artifactId = 'utils'
    publishVersion = rootProject.ext.utils_version_name
    desc = "the android lib that version is ${rootProject.ext.utils_version_name}"
    website = siteUrl
    repository = gitUrl
    autoPublish = true
    bintrayUser = user
    bintrayKey = key
}

